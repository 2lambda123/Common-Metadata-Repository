(ns cmr.common.cache
  "A system level cache based on clojure.core.cache library.
  Follows basic usage pattern as given in - https://github.com/clojure/core.cache/wiki/Using"
  (:require [cmr.common.log :as log :refer (debug info warn error)]
            [clojure.core.cache :as cc]))


(defn cache-lookup
  "Returns value of a key if present else it will create a cache entry with the suplied key and value
  generated by the supplied function."
  [cmr-cache key f]
  (-> (swap! (:atom cmr-cache)
             (fn [cache]
               (if (cc/has? cache key)
                 (cc/hit cache key)
                 (cc/miss cache key (f)))))
      (get key)))

(defn create-cache
  "Create system level cache."
  ([]
   (create-cache (cc/lru-cache-factory {})))
  ([initial-cache]
   {:initial initial-cache
    :atom (atom initial-cache)}))

(defn reset-cache
  [cmr-cache]
  (reset! (:atom cmr-cache) (:initial cmr-cache)))




