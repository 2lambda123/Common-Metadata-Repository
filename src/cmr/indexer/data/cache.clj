(ns cmr.indexer.data.cache
  "A system level cache based on clojure.core.cache library.
  Follows basic usage pattern as given in - https://github.com/clojure/core.cache/wiki/Using"
  (:require [cmr.common.log :as log :refer (debug info warn error)]
            [cmr.system-trace.context :as context]
            [clojure.core.cache :as cc]
            [cmr.indexer.data.index-set :as idx-set]))


(defn cache-lookup
  "Returns value of a key if present else it will create a cache entry with the suplied key and value
  generated by the supplied function."
  [cache-atom key f]
  (-> (swap! cache-atom
             (fn [cache]
               (if (cc/has? cache key)
                 (cc/hit cache key)
                 (cc/miss cache key (f)))))
      (get key)))

(defn create-cache
  "Create system level cache."
  []
  (atom (cc/lru-cache-factory {})))

(defn get-elastic-config
  "Fetch elastic config from the cache."
  [context]
  (let [cache-atom (-> context :system :cache)]
    (cache-lookup cache-atom :elastic-config #(idx-set/get-elastic-config))))

(defn get-concept-type-index-names
  "Fetch index names associated with concepts."
  [context]
  (let [cache-atom (-> context :system :cache)]
    (cache-lookup cache-atom :concept-indices #(idx-set/get-concept-type-index-names))))

(defn get-concept-mapping-types
  "Fetch mapping types associated with concepts."
  [context]
  (let [cache-atom (-> context :system :cache)]
    (cache-lookup cache-atom :concept-mapping-types #(idx-set/get-concept-mapping-types))))

(defn reset-cache
  [cache-atom]
  (reset! cache-atom (cc/lru-cache-factory {})))


